buildscript {
    ext {
        set('springBootVersion', '3.2.4')
        set('springCloudVersion', '2023.0.1')
        set('springCloudAlibabaVersion', '2023.0.1.2')
        set('redissonVersion', '3.31.0')
        set('cosVersion', '5.6.225')
        set('gsonVersion', '2.11.0')
        set('knife4jVersion', '4.4.0')
        set('springDocVersion', '2.5.0')
        set('jjwtVersion', '0.12.5')
        set('kaptchaVersion', '2.3.2')

        set('hibernatePluginVersion', '6.5.2.Final')
        set('graalvmNativePluginVersion', '0.10.2')
    }
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public/' }
        mavenCentral()
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    apply {
        plugin('java')
        plugin('java-library')
    }
    repositories {
        mavenLocal()
        mavenCentral()
    }
    group 'org.funcode'
    version '0.0.1-SNAPSHOT'
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

subprojects {
    apply {
        plugin('org.springframework.boot')
        plugin('io.spring.dependency-management')
    }
    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        annotationProcessor "org.hibernate.orm:hibernate-jpamodelgen:${hibernatePluginVersion}"
    }
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }
    tasks.register("prepareKotlinBuildScriptModel") {}
}
